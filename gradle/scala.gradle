apply plugin: 'java-library'
apply plugin: 'scala'

// A way to make Java code able to import Scala code (doesn't work by default)
ext.configureSources = { set, name ->
    set.scala.srcDir("src/$name/java")
    set.java.srcDirs = []
}
configureSources(sourceSets.main, 'main')
configureSources(sourceSets.test, 'test')

applyFile("scoverage")

dependencies {
    implementation platform(project(':dependencies'))

    api group: 'org.scala-lang', name: 'scala-library'

    // using "vintage" junit engine (junit 4) since scalameta doesn't support junit 5
    testCompileOnly group: 'org.scalameta', name: "munit_${scalaVersionMajor}"
    testRuntimeOnly group: 'org.junit.vintage', name: 'junit-vintage-engine'
}

applyFile("test")

gradle.projectsEvaluated {
    tasks.withType(Test) { task ->
        include '**/*Suite.class'
    }
}